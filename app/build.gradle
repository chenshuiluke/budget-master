apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    lintOptions {
        abortOnError false
    }

    def versionPropsFile = file('version.properties')
    def versionBuild

    /*Setting default value for versionBuild which is the last incremented value stored in the file */
    if (versionPropsFile.canRead()) {
        def Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        versionBuild = versionProps['VERSION_BUILD'].toInteger()
    } else {
        throw new GradleException("Could not read version.properties!")
    }

    /*Wrapping inside a method avoids auto incrementing on every gradle task run. Now it runs only when we build apk*/
    ext.autoIncrementBuildNumber = {

        if (versionPropsFile.canRead()) {
            def Properties versionProps = new Properties()
            versionProps.load(new FileInputStream(versionPropsFile))
            versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
            versionProps['VERSION_BUILD'] = versionBuild.toString()
            versionProps.store(versionPropsFile.newWriter(), null)
        } else {
            throw new GradleException("Could not read version.properties!")
        }
    }

    task increaseVersion() << {
        autoIncrementBuildNumber()

    }
    tasks.whenTaskAdded { task ->
        if (task.name == 'assembleRelease') {
            task.finalizedBy 'increaseVersion'
        }
    }

    defaultConfig {
        applicationId "com.lukechenshui.budgetmaster"
        minSdkVersion 16
        targetSdkVersion 16
        versionCode versionBuild
        versionName "1.0." + versionBuild
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
        }
        debug {
        }
    }
    packagingOptions{
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    testCompile 'junit:junit:4.10'
    compile 'com.github.Ritaja:java-exchange-rates:1.0'
    compile group: 'com.nineoldandroids', name: 'library', version: '2.4.0'
    compile 'com.github.aakira:expandable-layout:1.6.0@aar'
    compile 'com.android.support:recyclerview-v7:24.2.1'
    compile 'com.google.guava:guava:19.0'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    compile group: 'org.apache.httpcomponents', name: 'httpclient-android', version: '4.3.5.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:cardview-v7:24.0.+'
    compile 'com.android.support:recyclerview-v7:24.0.+'
    compile 'gun0912.ted:tedbottompicker:1.0.3'
    compile 'gun0912.ted:tedpermission:1.0.0'
}

configurations {
    compile.exclude group: "org.json", module: "json"
    compile.exclude group: "org.apache.httpcomponents", module: "httpcore"
    compile.exclude group: "org.apache.httpcomponents", module: "httpclient"
    compile.exclude group: "org.hamcrest", module: "hamcrest-core"
}